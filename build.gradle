plugins {
    id "com.diffplug.eclipse.apt" version "3.22.0"
    id("com.github.johnrengelman.shadow") version "7.0.0"
    id "nu.studer.jooq" version "4.2"
    id "application"
}

version "0.1"
group "example.jooq"

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://s01.oss.sonatype.org/content/repositories/snapshots/" }
}

configurations {
    // for dependencies that are needed for development only
    developmentOnly
}

dependencies {
    annotationProcessor(enforcedPlatform("io.micronaut:micronaut-bom:$micronautVersion"))
    annotationProcessor("io.micronaut:micronaut-inject-java")
    annotationProcessor("io.micronaut:micronaut-graal")
    annotationProcessor("io.micronaut:micronaut-validation")

    compileOnly(enforcedPlatform("io.micronaut:micronaut-bom:$micronautVersion"))
    compileOnly("org.graalvm.nativeimage:svm")

    implementation(enforcedPlatform("io.micronaut:micronaut-bom:$micronautVersion"))
    implementation("io.micronaut:micronaut-inject")
    implementation("io.micronaut:micronaut-validation")
    implementation("io.micronaut:micronaut-runtime")
    implementation("io.micronaut:micronaut-http-client")
    implementation("io.micronaut:micronaut-http-server-netty")
    implementation("io.micronaut.sql:micronaut-jooq")
    implementation("org.simpleflatmapper:sfm-jdbc:8.2.3")

    runtimeOnly("io.micronaut.sql:micronaut-jdbc-hikari")
    runtimeOnly("ch.qos.logback:logback-classic")
    runtimeOnly("com.h2database:h2")

    jooqRuntime(enforcedPlatform("io.micronaut:micronaut-bom:$micronautVersion"))
    jooqRuntime("com.h2database:h2")

    testAnnotationProcessor(enforcedPlatform("io.micronaut:micronaut-bom:$micronautVersion"))
    testAnnotationProcessor("io.micronaut:micronaut-inject-java")

    testImplementation(enforcedPlatform("io.micronaut:micronaut-bom:$micronautVersion"))
    testImplementation("org.junit.jupiter:junit-jupiter-api")
    testImplementation("io.micronaut.test:micronaut-test-junit5")

    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
}

test.classpath += configurations.developmentOnly

mainClassName = "example.jooq.Application"

// use JUnit 5 platform
test {
    useJUnitPlatform()
}

java {
    sourceCompatibility = JavaVersion.toVersion('1.8')
    targetCompatibility = JavaVersion.toVersion('1.8')
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.compilerArgs.addAll([
        '-parameters',
        // enables incremental compilation
        '-Amicronaut.processing.incremental=true',
        '-Amicronaut.processing.annotations=example.jooq.*',
        "-Amicronaut.processing.group=$project.group",
        "-Amicronaut.processing.module=$project.name",
    ])
}

shadowJar {
    mergeServiceFiles()
}

tasks.withType(JavaExec) {
    classpath += configurations.developmentOnly
    jvmArgs('-XX:TieredStopAtLevel=1', '-Dcom.sun.management.jmxremote')
    if (gradle.startParameter.continuous) {
        systemProperties(
            'micronaut.io.watch.restart': 'true',
            'micronaut.io.watch.enabled': 'true',
            "micronaut.io.watch.paths": "src/main"
        )
    }
}

jooq {
    version = '3.14.0'
    devDb(sourceSets.main) {
        jdbc {
            driver = 'org.h2.Driver'
            url = 'jdbc:h2:mem:devDb;MVCC=TRUE;LOCK_TIMEOUT=10000;DB_CLOSE_ON_EXIT=FALSE;INIT=CREATE TABLE IF NOT EXISTS owner(id INT AUTO_INCREMENT, name VARCHAR(255) NOT NULL, age INT NOT NULL, CONSTRAINT owner_pk PRIMARY KEY (id))\\; CREATE TABLE IF NOT EXISTS pet (id UUID DEFAULT random_uuid() NOT NULL, name VARCHAR(255) NOT NULL, owner_id INT NOT NULL, type VARCHAR(255) NOT NULL, CONSTRAINT pet_pk PRIMARY KEY (id), CONSTRAINT pet_owner_id_fk FOREIGN KEY (owner_id) REFERENCES owner)\\;'
            user = 'sa'
            password = ''
        }
        generator {
            name = 'org.jooq.codegen.DefaultGenerator'
            database {
                name = 'org.jooq.meta.h2.H2Database'
                inputSchema = 'PUBLIC'
            }
            target {
                packageName = "example.jooq.domain"
            }
            generate {
                jpaAnnotations = true
            }
            database {
                forcedTypes {
                    forcedType {
                        userType = 'example.jooq.domain.Pet.PetType'
                        converter = 'example.jooq.converters.PetTypeConverter'
                        includeExpression = 'PET\\.TYPE'
                    }
                }
            }
        }
    }
}
